DATA SEGMENT
       
;COLORS:

BLACK EQU 0
BLUE EQU 1
GREEN EQU 2 
CYAN EQU 3
RED EQU 4
MAGENTA EQU 5
BROWN EQU 6 
GRAY EQU 7
DARK_GRAY EQU 8
LIGHT_BLUE EQU 9
LIGHT_GREEN EQU 0AH
LIGHT_CYAN EQU 0BH
LIGHT_RED EQU 0CH 
LIGHT_MAGENTA EQU 0DH
YELLOW EQU 0EH
WHITE EQU 0FH
  
;SCREEN:
 
SCREEN_LENGTH EQU 320 
SCREEN_WIDTH EQU 200
GRAY_LINE DB 320 DUP(GRAY)
MIDDLE_LINE DB 12 DUP(GRAY),296 DUP(BLACK), 12 DUP(GRAY)
 
;PLAYER:
                                                      
DIFFICULTY_MASSAGE DB "PLEASE SELECT A DIFFICULTY LEVEL:", '$'
EASY DB "A-EASY",'$'
MEDIUM DB "B-MEDIUM",'$'
HARD DB "C-HARD",'$' 
GOAL_MASSAGE DB "YOUR GOAL IS TO BREAK ALL OF THE BRICKS",'$'
RIGHT_MASSAGE DB "PRESS D TO MOVE THE PADDLE TO THE RIGHT",'$'
LEFT_MASSAGE DB "PRESS A TO MOVE THE PADDLE TO THE LEFT",'$'
BREAKOUT DB "BREAKOUT",'$'  
START_MASSAGE DB "PRESS ANY KEY TO START THE GAME!",'$' 
WIN_MASSAGE DB "YOU WON! GOOD JOB!",'$'
END_MASSAGE DB "PRESS ANY KEY TO RESTART",'$'
ESCAPE_MASSAGE DB "PRESS ESCAPE TO LEAVE",'$'
LOSE_MASSAGE DB "YOU LOST, YOUR SCORE WAS ",'$'  
SCORE_NUM DB '0','0','$' 
HIGH_SCORE_NUM DB '0','0','$'
SCORE DB "SCORE:",'$'
HIGH_SCORE DB "HIGH SCORE:",'$' 
PAUSE_MASSAGE DB "PRESS P TO PAUSE AND UNPAUSE THE GAME ",'$' 
MUTE_MASSAGE DB "PRESS M TO MUTE AND UNMUTE THE GAME",'$'
PAUSE_SIGN DB 10 DUP (WHITE),10 DUP(BLACK), 10 DUP(WHITE)  
PAUSE_ERASE DB 30 DUP(BLACK)

TROPHY DB 8 DUP (YELLOW), 4 DUP(BLACK), 30 DUP(YELLOW), 4 DUP(BLACK), 8 DUP(YELLOW)  
DB 8 DUP (YELLOW), 4 DUP(BLACK), 30 DUP(YELLOW), 4 DUP(BLACK), 8 DUP(YELLOW)    
DB 2 DUP(YELLOW), 4 DUP(BLACK), 2 DUP(YELLOW), 4 DUP(BLACK), 30 DUP(YELLOW), 4 DUP(BLACK),2 DUP (YELLOW), 4 DUP(BLACK), 2 DUP (YELLOW)
DB 2 DUP(YELLOW), 4 DUP(BLACK), 2 DUP(YELLOW), 4 DUP(BLACK), 30 DUP(YELLOW), 4 DUP(BLACK),2 DUP (YELLOW), 4 DUP(BLACK), 2 DUP (YELLOW)
DB 2 DUP(YELLOW), 4 DUP(BLACK), 4 DUP(YELLOW), 2 DUP(BLACK),16 DUP(YELLOW),2 DUP(GRAY), 12 DUP(YELLOW), 2 DUP(BLACK), 4 DUP(YELLOW), 4 DUP(BLACK),2 DUP (YELLOW)
DB 2 DUP(YELLOW), 4 DUP(BLACK), 4 DUP(YELLOW), 2 DUP(BLACK),16 DUP(YELLOW),2 DUP(GRAY), 12 DUP(YELLOW), 2 DUP(BLACK), 4 DUP(YELLOW), 4 DUP(BLACK),2 DUP (YELLOW)
DB 2 DUP(YELLOW), 6 DUP(BLACK), 18 DUP(YELLOW), 4 DUP(GRAY), 16 DUP(YELLOW), 6 DUP(BLACK), 2 DUP(YELLOW)
DB 2 DUP(YELLOW), 6 DUP(BLACK), 18 DUP(YELLOW), 4 DUP(GRAY), 16 DUP(YELLOW), 6 DUP(BLACK), 2 DUP(YELLOW)
DB 2 DUP(YELLOW), 10 DUP(BLACK), 12 DUP(YELLOW), 6 DUP(GRAY), 12 DUP(YELLOW), 10 DUP(BLACK), 2 DUP(YELLOW)
DB 2 DUP(YELLOW), 10 DUP(BLACK), 12 DUP(YELLOW), 6 DUP(GRAY), 12 DUP(YELLOW), 10 DUP(BLACK), 2 DUP(YELLOW)  
DB 2 DUP(YELLOW), 10 DUP(BLACK), 10 DUP(YELLOW),8 DUP(GRAY), 12 DUP(YELLOW), 10 DUP(BLACK), 2 DUP(YELLOW)
DB 2 DUP(YELLOW), 10 DUP(BLACK), 10 DUP(YELLOW),8 DUP(GRAY), 12 DUP(YELLOW), 10 DUP(BLACK), 2 DUP(YELLOW)
DB 2 DUP(YELLOW), 10 DUP(BLACK), 14 DUP(YELLOW), 4 DUP(GRAY), 12 DUP(YELLOW), 10 DUP(BLACK),2 DUP(YELLOW) 
DB 2 DUP(YELLOW), 10 DUP(BLACK), 14 DUP(YELLOW), 4 DUP(GRAY), 12 DUP(YELLOW), 10 DUP(BLACK),2 DUP(YELLOW)
DB 4 DUP(YELLOW),8 DUP(BLACK), 14 DUP(YELLOW), 4 DUP(GRAY),12 DUP(YELLOW), 8 DUP(BLACK), 4 DUP(YELLOW)
DB 4 DUP(YELLOW),8 DUP(BLACK), 14 DUP(YELLOW), 4 DUP(GRAY),12 DUP(YELLOW), 8 DUP(BLACK), 4 DUP(YELLOW)
DB 2 DUP(BLACK), 2 DUP(YELLOW), 8 DUP(BLACK), 14 DUP(YELLOW),4 DUP(GRAY), 12 DUP(YELLOW), 8 DUP(BLACK), 2 DUP(YELLOW), 2 DUP(BLACK)
DB 2 DUP(BLACK), 2 DUP(YELLOW), 8 DUP(BLACK), 14 DUP(YELLOW),4 DUP(GRAY), 12 DUP(YELLOW), 8 DUP(BLACK), 2 DUP(YELLOW), 2 DUP(BLACK)
DB 2 DUP(BLACK), 4 DUP(YELLOW), 6 DUP(BLACK), 14 DUP(YELLOW),4 DUP(GRAY), 12 DUP(YELLOW), 6 DUP(BLACK), 4 DUP(YELLOW), 2 DUP(BLACK)
DB 2 DUP(BLACK), 4 DUP(YELLOW), 6 DUP(BLACK), 14 DUP(YELLOW),4 DUP(GRAY), 12 DUP(YELLOW), 6 DUP(BLACK), 4 DUP(YELLOW), 2 DUP(BLACK)
DB 4 DUP (BLACK), 6 DUP(YELLOW), 2 DUP(BLACK), 10 DUP(YELLOW),12 DUP(GRAY), 8 DUP(YELLOW), 2 DUP(BLACK), 6 DUP(YELLOW), 4 DUP(BLACK)
DB 4 DUP (BLACK), 6 DUP(YELLOW), 2 DUP(BLACK), 10 DUP(YELLOW),12 DUP(GRAY), 8 DUP(YELLOW), 2 DUP(BLACK), 6 DUP(YELLOW), 4 DUP(BLACK)
DB 8 DUP(BLACK), 14 DUP(YELLOW), 12 DUP(GRAY), 12 DUP(YELLOW), 8 DUP(BLACK)
DB 8 DUP(BLACK), 14 DUP(YELLOW), 12 DUP(GRAY), 12 DUP(YELLOW), 8 DUP(BLACK)
DB 12 DUP(BLACK), 30 DUP(YELLOW), 12 DUP(BLACK)
DB 12 DUP(BLACK), 30 DUP(YELLOW), 12 DUP(BLACK)
DB 14 DUP(BLACK), 26 DUP(YELLOW), 14 DUP(BLACK)
DB 14 DUP(BLACK), 26 DUP(YELLOW), 14 DUP(BLACK)
DB 16 DUP(BLACK),22 DUP(YELLOW), 16 DUP(BLACK)
DB 16 DUP(BLACK),22 DUP(YELLOW), 16 DUP(BLACK)
DB 20 DUP(BLACK), 14 DUP(YELLOW), 20 DUP(BLACK) 
DB 20 DUP(BLACK), 14 DUP(YELLOW), 20 DUP(BLACK)
DB 22 DUP(BLACK), 10 DUP(YELLOW), 22 DUP(BLACK) 
DB 22 DUP(BLACK), 10 DUP(YELLOW), 22 DUP(BLACK) 
DB 22 DUP(BLACK), 10 DUP(YELLOW), 22 DUP(BLACK)
DB 22 DUP(BLACK), 10 DUP(YELLOW), 22 DUP(BLACK) 
DB 22 DUP(BLACK), 10 DUP(YELLOW), 22 DUP(BLACK) 
DB 22 DUP(BLACK), 10 DUP(YELLOW), 22 DUP(BLACK)
DB 20 DUP(BLACK), 14 DUP(YELLOW), 20 DUP(BLACK)
DB 20 DUP(BLACK), 14 DUP(YELLOW), 20 DUP(BLACK)
DB 18 DUP(BLACK), 18 DUP(YELLOW), 18 DUP(BLACK)
DB 18 DUP(BLACK), 18 DUP(YELLOW), 18 DUP(BLACK)
DB 14 DUP(BLACK),26 DUP(YELLOW), 14 DUP(BLACK)
DB 14 DUP(BLACK),26 DUP(YELLOW), 14 DUP(BLACK)
DB 12 DUP(BLACK),30 DUP(YELLOW), 12 DUP(BLACK) 
DB 12 DUP(BLACK),30 DUP(YELLOW), 12 DUP(BLACK)
DB 6 DUP(BLACK), 42 DUP(BROWN), 6 DUP(BLACK)
DB 6 DUP(BLACK), 42 DUP(BROWN), 6 DUP(BLACK)
DB 6 DUP(BLACK),10 DUP(BROWN),22 DUP(GRAY), 10 DUP(BROWN), 6 DUP(BLACK) 
DB 6 DUP(BLACK),10 DUP(BROWN),22 DUP(GRAY), 10 DUP(BROWN), 6 DUP(BLACK)
DB 2 DUP(BLACK), 14 DUP(BROWN), 22 DUP(GRAY), 14 DUP(BROWN), 2 DUP(BLACK)
DB 2 DUP(BLACK), 14 DUP(BROWN), 22 DUP(GRAY), 14 DUP(BROWN), 2 DUP(BLACK)
DB 2 DUP(BLACK), 50 DUP(BROWN), 2 DUP(BLACK) 
DB 2 DUP(BLACK), 50 DUP(BROWN), 2 DUP(BLACK)       
 
;PADDLE:

PADDLE DB 40 DUP(BLUE)
PADDLE_ERASE DB 40 DUP (BLACK)
PADDLE_WIDTH EQU 5
PADDLE_LENGTH EQU 40 
PADDLE_Y EQU 176*320 ; CAN ONLY MOVE HORIZONTALY
PADDLE_X DW 144

;BALL:

BALL DB 3 DUP (0),3 DUP (RED), 3 DUP (0)  
DB 1 DUP (0),7 DUP (RED), 1 DUP (0)
DB 1 DUP (0),7 DUP (RED), 1 DUP (0)  
DB 9 DUP (RED)
DB 9 DUP (RED)
DB 9 DUP (RED)
DB 1 DUP (0),7 DUP (RED), 1 DUP (0)  
DB 1 DUP (0),7 DUP (RED), 1 DUP (0)
DB 3 DUP (0),3 DUP (RED), 3 DUP (0) 

BALL_ERASE DB 9 DUP (0)  
DB 9 DUP (0)
DB 9 DUP (0) 
DB 9 DUP (0)
DB 9 DUP (0)
DB 9 DUP (0)
DB 9 DUP (0)
DB 9 DUP (0)  
DB 9 DUP (0)
                           
BALL_WIDTH EQU 9 
BALL_X DW 160    
BALL_Y DW 167
BALL_SPEEDY DW  -1
BALL_SPEEDX DW  -1
COLLISION_SOUND DW 2950H  
EXTRA_DELAY DW 0 ;IF THERE HAS TO BE AN EXTRA DELAY TO SLOW THE BALL  
DIFFICULTY_DELAY DW 0
RESTART_SIGN DB 0

;BRICKS:  

BRICKS DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 295 DUP(BLACK)  
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN) 
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 295 DUP(BLACK)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 295 DUP(BLACK)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK, 36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN)
DB 295 DUP(BLACK)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)
DB 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK, 36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA),BLACK,36 DUP (LIGHT_CYAN),BLACK,36 DUP (LIGHT_MAGENTA)

BRICK_LOCATIONS DW 320*24+13,320*24+50,320*24+87,320*24+124,320*24+161,320*24+198,320*24+235,320*24+272
DW 320*36+13,320*36+50,320*36+87,320*36+124,320*36+161,320*36+198,320*36+235,320*36+272 
DW 320*48+13,320*48+50,320*48+87,320*48+124,320*48+161,320*48+198,320*48+235,320*48+272
DW 320*60+13,320*60+50,320*60+87,320*60+124,320*60+161,320*60+198,320*60+235,320*60+272 
DW 320*72+13,320*72+50,320*72+87,320*72+124,320*72+161,320*72+198,320*72+235,320*72+272

BRICK_ERASE DB 36 DUP (BLACK)
BRICKS_LENGTH EQU 295
BRICKS_WIDTH EQU 59  
LAST_BRICK_LOCATION DW ?

ENDS

STACK SEGMENT 
    
    DW   128  DUP(0)  
ENDS

CODE SEGMENT
     
PROC BRICK_COLLISION ;THIS FUNCTION CHECKS FOR A COLLISION WITH A BRICK AND BRAKES IT  
    PUSHA 
    ; LOOKING AT THE PIXEL OUTSIDE OF THE ENDS OF THE BALL TO CHECK FOR A COLLISION
    ; CHECKING THE MIDDLE LEFT PIXEL
    MOV AH,0DH
    MOV CX,BALL_X
    SUB CX,1 
    MOV DX,BALL_Y
    ADD DX,5
    INT 10H ; AL = COLOR
    CMP AL, 0
    JNZ LEFT_COLLISION
    
    ; CHECKING THE MIDDLE UP PIXEL
    MOV AH,0DH
    MOV CX,BALL_X 
    ADD CX, 5  
    MOV DX,BALL_Y
    INT 10H ; AL = COLOR
    CMP AL, 0
    JNZ UP_COLLISION 
    
    ; CHECKING THE MIDDLE RIGHT PIXEL
    MOV AH,0DH
    MOV CX,BALL_X
    ADD CX,9 
    MOV DX,BALL_Y
    ADD DX,5
    INT 10H ; AL = COLOR
    CMP AL, 0
    JNZ RIGHT_COLLISION
    
    ; CHECKING THE MIDDLE DOWN PIXEL
    MOV AH,0DH
    MOV CX,BALL_X
    ADD CX,5 
    MOV DX,BALL_Y
    ADD DX,9
    INT 10H ; AL = COLOR
    CMP AL, 0
    JNZ DOWN_COLLISION
     
    JMP FINISHED  ; IF THERE IS NO COLLISION, EXITING THE FUNCTION
    
LEFT_COLLISION: 
    NEG BALL_SPEEDX; CHANGING_DIRECTION
    MOV AX,1; SIGN OF THE LEFT COLLISION
    CALL LOCATE_BRICK
    MOV AX,LAST_BRICK_LOCATION
    MOV SI,AX 
    XOR DX,DX; DEVIDING DX,AX SO DX NEEDS TO BE ZERO TO DEVIDE ONLY AX
    MOV BX,320
    DIV BX ; AX HOLDS THE Y OF THE BRICK
    MOV BALL_Y,AX
    ADD BALL_Y,5
    MOV BALL_X,SI
    MUL BX ; AX HOLDS THE Y OF THE BRICK TIMES 320
    SUB BALL_X,AX  ; SUBTRACTING THE Y FROM THE POSITION OF THE BRICK, BX HOLDS THE BRICK'S X
    ADD BALL_X,36  ; COLLIDED WITH THE RIGHT SIDE OF THE BRICK
    PUSH SI
    CALL ERASE_BRICK 
    JMP FINISHED
     
RIGHT_COLLISION: 
    NEG BALL_SPEEDX; CHANGING_DIRECTION
    MOV AX,2   ; SIGN OF THE RIGHT COLLISION
    CALL LOCATE_BRICK
    MOV AX,LAST_BRICK_LOCATION
    MOV SI,AX 
    XOR DX,DX; DEVIDING DX,AX
    MOV BX,140H
    DIV BX   ;AX HOLDS THE Y OF THE BRICK
    MOV BALL_Y,AX
    ADD BALL_Y,5
    MOV BALL_X,SI
    MUL BX  ; AX HOLDS THE Y OF THE BRICK TIMES 320
    SUB BALL_X,AX ; SUBTRACTING THE Y FROM THE POSITION OF THE BRICK, BX HOLDS THE BRICK'S X
    SUB BALL_X,9  ; COLLIDED WITH THE LEFT SIDE OF THE BRICK
    PUSH SI
    CALL ERASE_BRICK
    JMP FINISHED
    
    
UP_COLLISION:
    NEG BALL_SPEEDY; CHANGING_DIRECTION
    MOV AX,3   ; SIGN OF THE UP COLLISION
    CALL LOCATE_BRICK
    MOV AX,LAST_BRICK_LOCATION
    MOV SI,AX
    XOR DX,DX; DEVIDING DX,AX
    MOV BX,320
    DIV BX  ; AX HOLDS THE Y OF THE BRICK 
    MOV BALL_Y,AX    
    ADD BALL_Y,13    ; COLLIDED WITH THE DOWN SIDE OF THE BRICK
    PUSH SI
    CALL ERASE_BRICK
    JMP FINISHED
    
DOWN_COLLISION:
    NEG BALL_SPEEDY; CHANGING_DIRECTION
    MOV AX,0 ; SIGN OF THE DOWN COLLISION
    CALL LOCATE_BRICK 
    MOV AX,LAST_BRICK_LOCATION
    MOV SI,AX
    XOR DX,DX; DEVIDING DX,AX
    MOV BX,140H
    DIV BX     ; AX HOLDS THE Y OF THE BRICK 
    MOV BALL_Y,AX
    SUB BALL_Y,1 ; COLLIDED WITH THE UP SIDE OF THE BRICK
    PUSH SI
    CALL ERASE_BRICK 
    
FINISHED:
    POPA
    RET
ENDP

PROC LOCATE_BRICK  ;THIS FUNCTION LOCATES WHAT BRICK DID THE BALL COLIDE WITH  AND STORES IT'S LOCATION IN LAST_BRICK_LOCATION
    PUSHA  
    CALL UPDATE_SCORE ; ADDING ONE TO THE SCORE  
    ;CHECKING WHAT TYPE OF COLLISION OCCURRED 
    CMP AX,1
    JZ LEFT
    CMP AX,2
    JZ RIGHT
    CMP AX,3
    JZ UP
   
   ; SETTING THE CHANGE TO THE VARIABLES OF THE BALL'S X AND THE BALL'S Y. SI-X DI-Y
DOWN:
    MOV SI, 5 ; CHECKING THE MIDDLE
    MOV DI, 9 ; CHECKING THE DOWN SIDE OF THE BALL
    JMP CHECK

LEFT:
    MOV SI, -1  ; CHECKING THE LEFT SIDE OF THE BALL
    MOV DI, 5   ; CHECKING THE MIDDLE
    JMP CHECK

RIGHT:
    MOV SI, 9   ; CHECKING THE RIGHT SIDE OF THE BALL
    MOV DI, 5   ; CHECKING THE MIDDLE
    JMP CHECK

UP:
    MOV SI, 5   ; CHECKING THE MIDDLE
    MOV DI, 1   ; CHECKING THE UP SIDE OF THE BALL
     
CHECK:
    ; TO CHECK WHAT BRICK THE BALL BREAKS, DEVIDING IT'S X AND Y TO GET TO THE RIGHT PLACE IN BRICK_LOCATIONS 
    XOR DX,DX
    XOR CX,CX          
    MOV AX,BALL_X 
    SUB AX,13; BRICKS START AT 13-X
    ADD AX,SI  ; CHANGING THE X THAT IT CHECKS ACCORDING TO WICH SIDE IT CHECKS
    MOV BL,37 ; EACH BRICK IS 36 AND THERE IS 1 BLACK PIXEL AFTER EAXH ONE
    DIV BL    ; AX HAS THE BRICK'S NUMBER IN ITS ROW
    MOV CL,AL
    MOV AX,2
    MUL CX  ; EACH LOCATION IS A WORD
    MOV CX,AX  ; CX HAS THE OFFSET OF THE BRICK'S LOCATION FROM THE START OF IT'S ROW, ITS COLLOM
    MOV AX,BALL_Y 
    SUB AX, 24; BRICKS START AT 24-Y
    ADD AX,DI ; CHANGING THE Y THAT IT CHECKS ACCORDING TO WICH SIDE IT CHECKS
    MOV BL,12 ; EACH BRICK IS 11 AND THERE IS A BLACK PIXEL AFTER EACH ONE
    DIV BL ; AX HAS THE BRICK'S NUMBER IN ITS COLLOM   
    MOV DL,AL
    MOV AX,16
    MUL DX   ; EACH LOCATION IS A WORD AND THERE ARE 8 BRICKS IN EACH ROW
    MOV DX,AX  ; DX HAS THE OFFSET OF THE BRICK'S LOCATION ONLY CONSIDERING THE ROW IT'S IN
    MOV BX,OFFSET BRICK_LOCATIONS
    ADD BX, CX 
    ADD BX,DX   
    MOV AX, [BX]
    MOV LAST_BRICK_LOCATION,AX
    POPA
    RET
ENDP

PROC DRAW_BRICKS ; THIS FUCTION DRAWS THE BRICKS
    PUSHA 
    MOV DI,320*24+13; CEILING ENDS AT 20 AND LEFT WALL ENDS AT 12
    MOV CX, BRICKS_WIDTH 
    MOV AX, BRICKS_LENGTH  
    MOV SI, OFFSET BRICKS 
REPEAT4: 
    PUSH AX
    PUSH SI
    PUSH DI
    CALL DRAW_LINE  
    ADD SI, AX
    ADD DI, 320
    LOOP REPEAT4
    POPA
    RET 
ENDP 

PROC DRAW_TROPHY ; THIS FUCTION DRAWS THE TROPHY
    PUSHA 
    MOV DI,320*135+133
    MOV CX, 54 
    MOV AX, 54  
    MOV SI, OFFSET TROPHY 
REPEAT5: 
    PUSH AX
    PUSH SI
    PUSH DI
    CALL DRAW_LINE  
    ADD SI, AX
    ADD DI, 320
    LOOP REPEAT5
    POPA
    RET 
ENDP

PROC ERASE_BRICK ; THIS FUCTION ERASES A BRICK
    MOV BP,SP
    PUSHA
    MOV AX, 11
    PUSH AX
    MOV AX,36
    PUSH AX
    MOV BX, OFFSET ERASE_BRICK
    PUSH BX
    MOV AX, [BP+2]
    PUSH AX
    CALL DRAW_RECTANGLE 
    POPA
    RET 2
ENDP
     
PROC INIT_GRAPHICS   ;SET TO GRAPHICS MODE
    PUSH AX
    MOV AX, 13H
    INT 10H
    MOV AX, 0A000H
    MOV ES, AX
    POP AX
    RET 
ENDP 
    
PROC DRAW_RECTANGLE  ; THIS FUNCTION DRAWS AND ERASES A RECTANGLE
    MOV BP,SP    
    PUSHA
    MOV CX, [BP + 8] ; ROWS TO DRAW
    MOV AX, [BP + 6] ; COLS TO DRAW
    MOV SI, [BP + 4] ; FROM WHERE TO COPY
    MOV DI, [BP + 2] ; PLACE ON SCREEN
REPEAT: 
    PUSH AX
    PUSH SI
    PUSH DI
    CALL DRAW_LINE  
    ADD DI, 320
    LOOP REPEAT
    POPA
    RET 8
ENDP 
                     
PROC DRAW_TOP_SCREEN ; THIS FUCTION DRAW THE TOP PART OF THE SCREEN (CEILING)
    PUSHA  
    MOV AX, 20
    PUSH AX
    MOV AX,320
    PUSH AX
    MOV AX, OFFSET GRAY_LINE
    PUSH AX
    XOR AX,AX
    PUSH AX
    CALL DRAW_RECTANGLE 
    POPA
    RET
ENDP  

PROC DRAW_MIDDLE_SCREEN  ; THIS FUCTION DRAWS THE MIDDLE PART OF THE SCREEN(WALLS)
    PUSHA  
    XOR AX,AX
    MOV AX, 180
    PUSH AX
    MOV AX,320
    PUSH AX
    MOV AX, OFFSET MIDDLE_LINE
    PUSH AX
    MOV AX,20*320
    PUSH AX
    CALL DRAW_RECTANGLE 
    POPA
    RET
ENDP 

PROC PLAY_COLISION_SOUND  ; THIS FUNCTION PLAYS THE SOUND OF THE COLLISION WITH A WALL OR THE PADDLE
    PUSHA  
    CMP COLLISION_SOUND,0
    JZ PLAYED ; IF THE SOUND IS ZERO, DON'T PLAY A SOUND
    IN AL, 61H
    OR AL, 00000011B
    OUT 61H, AL
    ; SEND CONTROL WORD TO CHANGE FREQUENCY
    MOV AL, 0B6H
    OUT 43H, AL
    ; PLAY FREQUENCY 131HZ
    MOV AX, [COLLISION_SOUND]
    OUT 42H, AL ; SENDING LOWER BYTE
    MOV AL, AH
    OUT 42H, AL ; SENDING UPPER BYTE  
    ;TIME UNTIL SOUND ENDS
    MOV CX, 1
    MOV DX, 1600H
    MOV AH, 86H
    INT 15H
    ; CLOSE THE SPEAKER
    IN AL, 61H
    AND AL, 11111100B
    OUT 61H, AL
     
PLAYED:
    POPA
    RET
ENDP

PROC DRAW_BALL ; THIS FUNCTION DRAWS THE BALL   
    PUSHA
    MOV DI, BALL_X ;PLACE ON SCREEN 
    MOV BX, BALL_Y
    MOV AX,320
    MUL BX
    ADD DI, AX
    MOV CX, BALL_WIDTH 
    MOV AX, BALL_WIDTH  
    MOV SI, OFFSET BALL 
REPEAT6: 
    PUSH AX
    PUSH SI
    PUSH DI
    CALL DRAW_LINE  
    ADD SI, AX
    ADD DI, 320
    LOOP REPEAT6
    POPA
    RET 
ENDP 

PROC ERASE_BALL ; THIS FUNCTION ERASES THE BALL  
    PUSHA 
    MOV DI, BALL_X ;PLACE ON SCREEN
    MOV AX,320
    MOV BX,BALL_Y
    MUL BX
    ADD DI, AX
    MOV CX, BALL_WIDTH 
    MOV AX, BALL_WIDTH  
    MOV SI, OFFSET BALL_ERASE 
REPEAT7: 
    PUSH AX
    PUSH SI
    PUSH DI
    CALL DRAW_LINE  
    ADD SI, AX
    ADD DI, 320
    LOOP REPEAT7
    POPA
    RET 
ENDP

PROC DRAW_LINE       ;THIS FUNCTION DRAWS A LINE (ALSO ERASES A LINE)
    MOV BP, SP
    PUSHA
    MOV CX, [BP + 6] ; COLS TO DRAW
    MOV SI, [BP + 4] ; FROM WHERE TO COPY
    MOV DI, [BP + 2] ; TO WHERE TO COPY
    REP MOVSB
    POPA
    RET 6
ENDP 

PROC DRAW_PADDLE  ; THIS FUNCTION DRAWS THE PADDLE
    PUSHA
    PUSH PADDLE_WIDTH
    PUSH PADDLE_LENGTH  
    MOV AX, OFFSET PADDLE 
    PUSH AX
    MOV AX,PADDLE_X
    ADD AX, PADDLE_Y
    PUSH AX
    CALL DRAW_RECTANGLE  
    POPA
    RET
ENDP 

PROC ERASE_PADDLE ; THIS FUNCTION ERASES THE PADDLE   
    PUSHA 
    PUSH PADDLE_WIDTH
    PUSH PADDLE_LENGTH  
    MOV AX, OFFSET PADDLE_ERASE 
    PUSH AX
    MOV AX,PADDLE_X
    ADD AX, PADDLE_Y
    PUSH AX
    CALL DRAW_RECTANGLE 
    POPA
    RET
ENDP

PROC MOVEMENT
    PUSHA
UPDATE: 
   CMP SCORE_NUM, '4','0'
   JZ WON
   ; TO MAKE THE GAME HARDER AS YOU ADVANCE, THE DELAY BETWEEN EACH DRAWING OF THE BALL WILL DECREASE BY 65H TIMES THE SCORE, THIS WILL MAKE THE BALL MOVE FASTER 
    MOV DI, OFFSET SCORE_NUM
    MOV BL, [DI+1]
    SUB BL, '0'   ; GETTING THE ONE'S DIGIT 
    MOV CL, [DI]
    SUB CL, '0'   ; GETTING THE TEN'S DIGIT
    MOV AL,10H
    MUL CL        ; MULTIPLYING THE TEN'S DIGIT BY TEN TO GET THE ACTUALL NUMBER
    ADD BX,AX     ; THE RESULT WENT TO AX 
    MOV AL,25H
    MUL BL        ; MULTIPLYING THE SCORE BY TEN TO GET THE TIME TO SUBTRACT FROM THE DELAY
    MOV BX,AX     ; THE RESULT WENT TO AX
    
    MOV DI, EXTRA_DELAY
    MOV AX,DI     ; PUTING THE VALUE OF THE EXTRA DELAY IN AX
    SUB AX,BX 
    ADD AX, DIFFICULTY_DELAY; CHANGING THE DELAY FOR EACH DIFFICULTY
    MOV CX, 0     ; DELAYING THE START OF THE UPDATE SO THE BALL WONT MOVE TOO FAST
    MOV DX, 3000H
    ADD DX,AX     ; ADDING (OR SUBTRACTING) THE EXTRA DELAY TO THE DELAY TIME
    MOV AH, 86H
    INT 15H
    
    MOV AH, 1
    INT 16H    ;CHECKING FOR A KEY PRESS
    JZ BALL_MOVEMENT ; IF THERE WAS NO KEY PRESS, MOVE THE BALL
    MOV AH, 0
    INT 16H ; READ THE KEY PRESS
    CMP AL, 27
    JZ END_GAME ;IF THE ESCAPE BOTTON WAS PRESSED, LEAVE THE GAME
    CMP AL, 'd'
    JZ GO_RIGHT
    CMP AL, 'a'
    JZ GO_LEFT
    CMP AL, 'D'
    JZ GO_RIGHT
    CMP AL,'A' 
    JZ GO_LEFT
    CMP AL, 'P'
    JZ PAUSE
    CMP AL, 'p'
    JZ PAUSE
    CMP AL,'M'
    JZ MUTE
    CMP AL, 'm'
    JZ MUTE 
    JMP BALL_MOVEMENT
         
GO_RIGHT:
    CALL ERASE_PADDLE
    ADD PADDLE_X, 11
    CMP PADDLE_X, 268; CHECKING IF THE PADDLE REACHED THE END OF THE BOARD
    JG  ILLEGAL_RIGHT
    JMP MOVING
ILLEGAL_RIGHT:
    SUB PADDLE_X,11  ;CANCELING THE MOVEMENT
    JMP MOVING
    
GO_LEFT:   
    CALL ERASE_PADDLE
    SUB PADDLE_X, 11
    CMP PADDLE_X, 12 ; CHECKING IF THE PADDLE REACHED THE END OF THE BOARD
    JL  ILLEGAL_LEFT
    JMP MOVING
ILLEGAL_LEFT:
    ADD PADDLE_X,11 ; CANCELING THE MOVEMENT
    JMP MOVING
     
MOVING: 
    CALL DRAW_PADDLE 
    XOR BX,BX 

BALL_MOVEMENT:
    CALL ERASE_BALL  
    MOV BX, BALL_SPEEDY 
    ADD BALL_Y, BX          ;MOVING THE BALL VERTICALLY 
    MOV BX, BALL_SPEEDX     
    ADD BALL_X ,BX          ; MOVING THE BALL HORIZONTALLY  
    
    ; CHECKING FOR X AND Y RANGES THAT ARE A COLLISION OR A POTENTIAL COLLISION:
   
    CMP BALL_Y,SCREEN_WIDTH ;CHECKING IF THE BALL'S Y VALUE IS OUTSIDE OF THE SCREEN DOWNWAEDS
    JG  LOST 
    CMP BALL_Y,20           ;CHECKING IF THE BALL'S Y VALUE IS OUTSIDE OF THE SCREEN UPWARDS 
    JL CEILING_COLLISION 
    CMP BALL_X,13           ;CHECKING IF THE BALL'S X VALUE IS OUTSIDE OF THE SCREEN TO THE LEFT
    JL  LEFT_WALL_COLLISION         
    CMP BALL_X,298          ;CHECKING IF THE BALL'S X VALUE IS OUTSIDE OF THE SCREEN TO THE RIGHT
    JG  RIGHT_WALL_COLLISION      
    CMP BALL_Y,167          ; CHECKING FOR A POTENTIAL COLLISION WITH THE PADDLE
    JZ  PADDLE_COLLISION_CHECK    
    CMP BALL_Y,168          ; CHECKING FOR A POTENTIAL COLLISION WITH THE PADDLE
    JZ  PADDLE_COLLISION_CHECK    
    CMP BALL_Y,169  
    JZ  PADDLE_COLLISION_CHECK           
    CMP BALL_Y,86           ; CHECKING FOR A POTENTIAL COLLISION WITH A BRICK, THE BRICKS END AT 64          
    JL  BRICK_COLLISION_CHECK 
      
    JMP MOVE ;REGULAR MOVE 
    
LEFT_WALL_COLLISION:
    CALL PLAY_COLISION_SOUND  
    NEG BALL_SPEEDX  ;CHANGING DIRECTION 
    MOV BALL_X,12    ; PUTTING THE BALL ON THE LEFT WALL
    JMP MOVE  
    
RIGHT_WALL_COLLISION:
    CALL PLAY_COLISION_SOUND 
    NEG BALL_SPEEDX ;CHANGING DIRECTION
    MOV BALL_X,298  ; PUTTING THE BALL ON THE RIGHT WALL
    JMP MOVE
    
CEILING_COLLISION:
    CALL PLAY_COLISION_SOUND 
    NEG BALL_SPEEDY  ;CHANGING DIRECTION
    MOV BALL_Y,22    ; PUTTING THE BALL ON THE CEILING
    JMP MOVE
    
PADDLE_COLLISION_CHECK:  
    MOV BX,PADDLE_X
    SUB BX,9        ;THE BALL'S LENGTH IS 9
    CMP BALL_X,BX   ; CHECKING IF THE BALL'S X IS BIGGER THEN THE PADDLE'S X
    JL MOVE         ; IF THE BALL'S X IS LOWER THEN THE PADDLE'S X, THERE IS NO COLISION   
    ADD BX,27       ; HALF OF THE PADDLE'S LENGTH
    CMP BALL_X,BX  
    JBE FIRST_HALF_COLISION
    ADD BX,24       ; HALF OF THE PADDLE'S LENGTH+ COULD BE VERY CLOSE TO HITTING AND AFTERWARDS IT WILL GO INTO THE PADDLE
    CMP BALL_X,BX  
    JBE SECOND_HALF_COLISION
    JMP MOVE
    
FIRST_HALF_COLISION: 
    CALL PLAY_COLISION_SOUND 
    MOV BALL_Y,166   ; PUTTING THE BALL ON THE PADDLE
    CMP BALL_SPEEDY, 1
    JZ FIRST_ONE
    JMP FIRST_TWO
   
FIRST_ONE: 
    CMP BALL_SPEEDX,2
    JZ CHANGE_ONE_NONE  ; IF SPEEDX WAS 2 AND SPEEDY WAS 1, CHANGE TO SPEEDX 1 AND SPEEDY -1
    CMP BALL_SPEEDX,1
    JZ CHANGE_TWO_NONE  ; IF SPEEDX WAS 1 AND SPEEDY WAS 1, CHANGE TO SPEEDX 2 AND SPEEDY -1
    CMP BALL_SPEEDX,-1
    JZ CHANGE_NONE_NTWO ; IF SPEEDX WAS -1 AND SPEEDY WAS 1, CHANGE TO SPEEDX -1 AND SPEEDY -2
    CMP BALL_SPEEDX,-2
    JZ CHANGE_NONE_NONE ; IF SPEEDX WAS -2 AND SPEEDY WAS 1, CHANGE TO SPEEDX -2 AND SPEEDY -1 
   
FIRST_TWO: ; THE VELOCITIES CAN'T BE BOTH 2
    CMP BALL_SPEEDX,1
    JZ CHANGE_ONE_NONE  ; IF SPEEDX WAS 1 AND SPEEDY WAS 2, CHANGE TO SPEEDX 1 AND SPEEDY -1
    CMP BALL_SPEEDX,-1
    JZ CHANGE_NTWO_NONE ; IF SPEEDX WAS - AND SPEEDY WAS 2, CHANGE TO SPEEDX 1 AND SPEEDY -1
   
SECOND_HALF_COLISION: 
    CALL PLAY_COLISION_SOUND 
    MOV BALL_Y,166   ; PUTTING THE BALL ON THE PADDLE
    CMP BALL_SPEEDY, 1
    JZ SECOND_ONE
    JMP SECOND_TWO 
   
SECOND_ONE: 
    CMP BALL_SPEEDX,2
    JZ CHANGE_ONE_NONE ; IF SPEEDX WAS 2 AND SPEEDY WAS 1, CHANGE TO SPEEDX 1 AND SPEEDY -1
    CMP BALL_SPEEDX,1
    JZ CHANGE_ONE_NTWO ; IF SPEEDX WAS 1 AND SPEEDY WAS 1, CHANGE TO SPEEDX 1 AND SPEEDY -2
    CMP BALL_SPEEDX,-1
    JZ CHANGE_NTWO_NONE ; IF SPEEDX WAS -1 AND SPEEDY WAS 1, CHANGE TO SPEEDX -2 AND SPEEDY -1
    CMP BALL_SPEEDX,-2
    JZ CHANGE_NONE_NTWO  ; IF SPEEDX WAS -2 AND SPEEDY WAS 1, CHANGE TO SPEEDX -1 AND SPEEDY -2 
   
SECOND_TWO: ; THE VELOCITIES CAN'T BE BOTH 2
    CMP BALL_SPEEDX,1
    JZ CHANGE_ONE_NONE ; IF SPEEDX WAS 1 AND SPEEDY WAS 2, CHANGE TO SPEEDX 1 AND SPEEDY -1
    CMP BALL_SPEEDX,-1
    JZ CHANGE_NONE_NTWO  ; IF SPEEDX WAS - AND SPEEDY WAS 2, CHANGE TO SPEEDX -1 AND SPEEDY -2   
   
CHANGE_ONE_NONE:
    MOV BALL_SPEEDX,1
    MOV BALL_SPEEDY,-1
    MOV EXTRA_DELAY,0
    JMP MOVE
   
CHANGE_NONE_NONE:
    MOV BALL_SPEEDX,-1
    MOV BALL_SPEEDY,-1
    MOV EXTRA_DELAY,0
    JMP MOVE
   
CHANGE_ONE_NTWO:
    MOV BALL_SPEEDX,1
    MOV BALL_SPEEDY,-2 
    MOV EXTRA_DELAY,1960H
    JMP MOVE 
   
CHANGE_TWO_NONE:
    MOV BALL_SPEEDX,2
    MOV BALL_SPEEDY,-1
    MOV EXTRA_DELAY,1960H
    JMP MOVE
   
CHANGE_NONE_NTWO:
    MOV BALL_SPEEDX,-1
    MOV BALL_SPEEDY,-2
    MOV EXTRA_DELAY,1960H
    JMP MOVE
   
CHANGE_NTWO_NONE:
    MOV BALL_SPEEDX,-2
    MOV BALL_SPEEDY,-1
    MOV EXTRA_DELAY,1960H
    JMP MOVE
            
LOST:
    CALL LOSE
    JMP END_GAME 

WON:
    CALL WIN 
    JMP END_GAME
    
BRICK_COLLISION_CHECK:
    CALL BRICK_COLLISION
    JMP MOVE  

PAUSE: 
     CALL DRAW_PAUSE
     MOV AH,7 
     INT 21H 
     CMP AL,'P'
     JZ PLAY
     CMP AL, 'p'
     JZ PLAY
     JMP PAUSE

PLAY:   
     MOV CX,4  ; DELAYING THE READING OF THE KEY TO PREVENT THE PLAYER FROM ACCIDENTALY RESTARTING THE GAME
     MOV DX, 8350H 
     MOV AH, 86H
     INT 15H   
     CALL ERASE_PAUSE
     JMP UPDATE  

MUTE:
    CMP COLLISION_SOUND,0
    JZ UNMUTE
    MOV COLLISION_SOUND,0
    JMP UPDATE 

UNMUTE:
    MOV COLLISION_SOUND,2950H
    JMP UPDATE
        
MOVE:
    CALL DRAW_BALL
    JMP UPDATE   ; REPEATING THE MOVING AND CHECKING 

END_GAME:  
    POPA
    RET
ENDP  

PROC WRITE_SCORE 
    PUSHA
    MOV AH,2 ;SET CURSOR POSITION
    MOV BH,0 ;SET PAGE NUMBER
    MOV DH,0 ;SET ROW
    MOV DL,1 ;SET COLUM
    INT 10H
    LEA DX, SCORE
    MOV AH,9
    INT 21H  ; PRINTING THE SCORE MASSAGE
    LEA DX, SCORE_NUM
    MOV AH,9
    INT 21H   ; PRINTING THE SCORE NUMBER
    POPA
    RET 
ENDP

PROC WRITE_HIGH_SCORE 
    PUSHA
    MOV AH,2 ;SET CURSOR POSITION
    MOV BH,0 ;SET PAGE NUMBER
    MOV DH,0 ;SET ROW
    MOV DL,10 ;SET COLUM
    INT 10H
    LEA DX, HIGH_SCORE
    MOV AH,9
    INT 21H  ; PRINTING THE SCORE MASSAGE
    LEA DX, HIGH_SCORE_NUM
    MOV AH,9
    INT 21H   ; PRINTING THE SCORE NUMBER
    POPA
    RET 
ENDP

PROC UPDATE_SCORE 
    PUSHA 
    MOV BX, OFFSET SCORE_NUM 
    CMP [BX+1], '9'; COMPERING THE ONE'S DIGIT TO 9
    JZ ADD_TO_FIRST_DIGIT
    INC [BX+1]     ; IF ITS NOT 9, ADDING 1
    JMP DONE
        
ADD_TO_FIRST_DIGIT:
    MOV [BX+1],'0' ; CHANGING THE ONE'S DIGIT TO 0
    ADD [BX], 1    ; ADDING 1 TO THE TEN'S DIGIT    

DONE:
    CALL WRITE_SCORE
    POPA
    RET
ENDP

PROC DISPLAY_START_MASSAGES
    PUSHA 
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,6 ;SET ROW
    MOV DL,16 ;SET COLUM
    INT 10H
    LEA DX, BREAKOUT
    MOV AH,9
    INT 21H   ; PRINTING THE MASSAGE 
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,10 ;SET ROW
    MOV DL,1 ;SET COLUM
    INT 10H
    LEA DX, GOAL_MASSAGE
    MOV AH,9
    INT 21H   ; PRINTING THE MASSAGE 
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,12 ;SET ROW
    MOV DL,1 ;SET COLUM
    INT 10H
    LEA DX, LEFT_MASSAGE
    MOV AH,9
    INT 21H   ; PRINTING THE MASSAGE
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,14 ;SET ROW
    MOV DL,1 ;SET COLUM
    INT 10H
    LEA DX, RIGHT_MASSAGE
    MOV AH,9
    INT 21H   ; PRINTING THE MASSAGE    
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,16 ;SET ROW
    MOV DL,1 ;SET COLUM
    INT 10H
    LEA DX, PAUSE_MASSAGE
    MOV AH,9
    INT 21H   ; PRINTING THE MASSAGE 
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,18 ;SET ROW
    MOV DL,1 ;SET COLUM
    INT 10H
    LEA DX, MUTE_MASSAGE
    MOV AH,9
    INT 21H   ; PRINTING THE MASSAGE
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,22 ;SET ROW
    MOV DL,5 ;SET COLUM
    INT 10H
    LEA DX, START_MASSAGE
    MOV AH,9
    INT 21H   ; PRINTING THE MASSAGE
     
    MOV AH,7
    INT 21H
    POPA
    RET
ENDP 

PROC LOSE 
    PUSHA
    CALL INIT_GRAPHICS  
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,10 ;SET ROW
    MOV DL,6  ;SET COLUM
    INT 10H 
    
    LEA DX, LOSE_MASSAGE 
    MOV AH,9
    INT 21H   ; PRINTING THE LOSE MASSAGE    
    
    LEA DX,SCORE_NUM
    MOV AH,9
    INT 21H   ;PRINTING THE SCORE AT THE END
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,12 ;SET ROW
    MOV DL,6  ;SET COLUM
    INT 10H 
    LEA DX, END_MASSAGE  
    
    MOV AH,9
    INT 21H   ; PRINTING THE END MASSAGE 
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,14 ;SET ROW
    MOV DL,6  ;SET COLUM
    INT 10H 
    LEA DX, ESCAPE_MASSAGE 
    
    MOV AH,9
    INT 21H   ; PRINTING THE ESCAPE MASSAGE
    MOV CX,20  ; DELAYING THE READING OF THE KEY TO PREVENT THE PLAYER FROM ACCIDENTALY RESTARTING THE GAME
    MOV DX, 8350H 
    MOV AH, 86H
    INT 15H
    MOV AH,7
    INT 21H
    CMP AL, 27
    JZ EXIT
    MOV RESTART_SIGN,1
    ;IF THE ESCAPE BOTTON WAS PRESSED, LEAVE THE GAME 
    CALL INIT_GRAPHICS ; ERASING EVERYTHING ON THE SCREEN 
    MOV PADDLE_X, 144  ;RESTORING ALL CHANGED PARAMETERS VALUES
    MOV BALL_X,164
    MOV BALL_Y,167
    MOV BALL_SPEEDY, -1
    MOV BALL_SPEEDX,1 
    CALL SET_HIGH_SCORE
    MOV BX,OFFSET SCORE_NUM
    MOV [BX],'0'
    MOV [BX+1],'0'
    MOV EXTRA_DELAY,0  
     
EXIT:
    POPA
    RET
ENDP 
     
PROC WIN 
    PUSHA  
    CALL INIT_GRAPHICS 
    CALL DRAW_TROPHY
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,10 ;SET ROW
    MOV DL,6  ;SET COLUM
    INT 10H
    LEA DX, WIN_MASSAGE 
    MOV AH,9
    INT 21H   ; PRINTING THE LOSE MASSAGE
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,12 ;SET ROW
    MOV DL,6  ;SET COLUM
    INT 10H 
    LEA DX, END_MASSAGE
    MOV AH,9
    INT 21H   ; PRINTING THE END MASSAGE 
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,14 ;SET ROW
    MOV DL,6  ;SET COLUM
    INT 10H 
    LEA DX, ESCAPE_MASSAGE
    MOV AH,9
    INT 21H   ; PRINTING THE ESCAPE MASSAGE
     
    MOV CX,25  ; DELAYING THE READING OF THE KEY TO PREVENT THE PLAYER FROM ACCIDENTALY RESTARTING THE GAME
    MOV DX, 8350H 
    MOV AH, 86H
    INT 15H
    MOV AH,7
    INT 21H
    CMP AL, 27
    JZ EXIT1 ;IF THE ESCAPE BOTTON WAS PRESSED, LEAVE THE GAME 
    MOV RESTART_SIGN,1
    CALL INIT_GRAPHICS ; ERASING EVERYTHING ON THE SCREEN 
    MOV PADDLE_X, 144  ;RESTORING ALL CHANGED PARAMETERS VALUES
    MOV BALL_X,164
    MOV BALL_Y,167
    MOV BALL_SPEEDY, -1
    MOV BALL_SPEEDX,1  
    CALL SET_HIGH_SCORE 
    MOV BX,OFFSET SCORE_NUM
    MOV [BX],'0'
    MOV [BX+1],'0'
    MOV EXTRA_DELAY,0  
        
    EXIT1: 
    POPA 
    RET
ENDP 
 
PROC SET_HIGH_SCORE
    PUSHA 
    MOV BX,OFFSET SCORE_NUM
    MOV CX,[BX+1]
    SUB CX, '0'
    MOV AX, [BX]
    SUB AX, '0'
    MOV BL,10
    MUL BL
    ADD AX, CX
    MOV DX,AX
     
    MOV BX,OFFSET HIGH_SCORE_NUM
    MOV CX,[BX+1]
    SUB CX, '0'
    MOV AX, [BX]
    SUB AX, '0'
    MOV BL,10
    MUL BL
    ADD AX, CX
    CMP DX,AX
    JA CHANGE
    JMP DONE3 
    
CHANGE:
    MOV SI,OFFSET SCORE_NUM
    MOV BX,OFFSET HIGH_SCORE_NUM
    MOV AX, [SI]
    MOV [BX],AX 
    MOV AX,[SI+1]
    MOV [BX+1],AX 
    
DONE3: 
    POPA
    RET
ENDP

PROC RANDOM_START_SPEED ; STARTING THE GAME WITH A RANDOM SPEED
    PUSHA
    MOV AH, 00H  ; INTERRUPTS TO GET SYSTEM TIME        
    INT 1AH      ; CX:DX NOW HOLD NUMBER OF CLOCK TICKS SINCE MIDNIGHT      
    MOV AX,DX
    XOR DX,DX
    MOV CX,7    
    DIV CX      ; HERE DX CONTAINS THE REMAINDER OF THE DIVISION - FROM 0 TO 6 
    CMP DX, 0
    JZ CHANGE_NONE_NONE1
    CMP DX, 1
    JZ CHANGE_ONE_NTWO1
    CMP DX, 2
    JZ CHANGE_TWO_NONE1
    CMP DX, 3
    JZ CHANGE_NONE_NTWO1
    CMP DX, 4
    JZ CHANGE_NTWO_NONE1
    
CHANGE_ONE_NONE1:
    MOV BALL_SPEEDX,1
    MOV BALL_SPEEDY,-1
    MOV EXTRA_DELAY,0
    JMP DONE1
   
CHANGE_NONE_NONE1:
    MOV BALL_SPEEDX,-1
    MOV BALL_SPEEDY,-1
    MOV EXTRA_DELAY,0
    JMP DONE1
   
CHANGE_ONE_NTWO1:
    MOV BALL_SPEEDX,1
    MOV BALL_SPEEDY,-2 
    MOV EXTRA_DELAY,1960H
    JMP DONE1 
   
CHANGE_TWO_NONE1:
    MOV BALL_SPEEDX,2
    MOV BALL_SPEEDY,-1
    MOV EXTRA_DELAY,1960H
    JMP DONE1
   
CHANGE_NONE_NTWO1:
    MOV BALL_SPEEDX,-1
    MOV BALL_SPEEDY,-2
    MOV EXTRA_DELAY,1960H
    JMP DONE1
   
CHANGE_NTWO_NONE1:
    MOV BALL_SPEEDX,-2
    MOV BALL_SPEEDY,-1
    MOV EXTRA_DELAY,1960H 
   
DONE1:
    POPA
    RET
ENDP

PROC SELECT_DIFFICULTY  ; THIS FUNCTION GETS WHAT DIFFICULTY THE PLAYER WANTS TO PLAY IN AND CHANGES THE SPEED OF THE GAME ACCORDING TO IT
    PUSHA
    CALL INIT_GRAPHICS
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,7 ;SET ROW
    MOV DL,1  ;SET COLUM
    INT 10H
    LEA DX, DIFFICULTY_MASSAGE 
    MOV AH,9
    INT 21H   ; PRINTING THE LOSE MASSAGE
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,10 ;SET ROW
    MOV DL,6  ;SET COLUM
    INT 10H 
    LEA DX, EASY
    MOV AH,9
    INT 21H   ; PRINTING THE END MASSAGE 
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,13 ;SET ROW
    MOV DL,6  ;SET COLUM
    INT 10H 
    LEA DX, MEDIUM
    MOV AH,9
    INT 21H   ; PRINTING THE ESCAPE MASSAGE  
    
    MOV AH,2  ;SET CURSOR POSITION
    MOV BH,0  ;SET PAGE NUMBER
    MOV DH,16 ;SET ROW
    MOV DL,6  ;SET COLUM
    INT 10H 
    LEA DX, HARD
    MOV AH,9
    INT 21H   ; PRINTING THE ESCAPE MASSAGE
    
CHECK2: 
    MOV AH,7
    INT 21H
    CMP AL, 'A'
    JZ SET_EASY
    CMP AL, 'a'
    JZ SET_EASY
    CMP AL, 'B'
    JZ SET_MEDIUM
    CMP AL, 'b'
    JZ SET_MEDIUM
    CMP AL, 'C'
    JZ SET_HARD
    CMP AL, 'c'
    JZ SET_HARD 
    JMP CHECK2 ; IF THE KEY PRESS WAS INVALID READING ANOTHER KEY

SET_EASY:
    MOV DIFFICULTY_DELAY,700H; ADDING A DELAY, MAKING THE GAME SLOWER
    JMP DONE4
SET_MEDIUM: 
    MOV DIFFICULTY_DELAY,300H; ADDING A DELAY, MAKING THE GAME SLOWER 
    JMP DONE4
SET_HARD:
    MOV DIFFICULTY_DELAY, -600H ; SUBTRACTING FROM THE DELAY, MAKING THE GAME FASTER
    
DONE4:   
    POPA
    RET
ENDP 

PROC DRAW_PAUSE ; THIS FUNCTION DRAWS THE PAUSE SIGN
    PUSHA
    MOV AX, 30
    PUSH AX
    MOV AX,30
    PUSH AX
    MOV AX, OFFSET PAUSE_SIGN
    PUSH AX
    MOV AX,105*320+145
    PUSH AX
    CALL DRAW_RECTANGLE
    POPA
    RET
ENDP 

PROC ERASE_PAUSE  ; THIS FUCTION ERASES THE PAUSE SIGN
    PUSHA 
    MOV AX, 30
    PUSH AX
    MOV AX,30
    PUSH AX
    MOV AX, OFFSET PAUSE_ERASE
    PUSH AX
    MOV AX,105*320+145
    PUSH AX
    CALL DRAW_RECTANGLE
    POPA
    RET
ENDP 

START:      

; SET SEGMENT REGISTERS: 
    MOV AX,DATA
    MOV DS,AX 
    
START_GAME:
    CALL INIT_GRAPHICS
    MOV RESTART_SIGN,0 
    CALL DISPLAY_START_MASSAGES 
    CALL SELECT_DIFFICULTY
    MOV CX, 8 ; DELAYING THE START OF THE GAME TO ALLOW THE PLAYER TO GET READY
    MOV DX, 2350H 
    MOV AH, 86H
    INT 15H   
    
    CALL DRAW_TOP_SCREEN  
    CALL DRAW_MIDDLE_SCREEN
    CALL WRITE_SCORE 
    CALL WRITE_HIGH_SCORE
    CALL DRAW_BALL 
    CALL DRAW_PADDLE
    CALL DRAW_BRICKS
    CALL RANDOM_START_SPEED  
    CALL MOVEMENT   
    CALL INIT_GRAPHICS
    CMP RESTART_SIGN,1 ; IF THE PLAYER CHOSE TO RESTART, RESTART THE GAME
    JZ START_GAME 
    
    MOV AX, 4C00H; EXIT TO OPERATING SYSTEM
    INT 21H    
ENDS

END START
